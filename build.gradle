buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = getVersionFromJava()
group = mod_group
archivesBaseName = mod_id

configurations.configureEach {
    exclude group: "net.minecraftforge", module: "mergetool"
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: mappings_channel, version: mappings_version
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    accessTransformer = file('src/main/resources/META-INF/at_deps.cfg')
	    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
        }
    }
}

//Returns the current version by reading directly from the ModInfo.java file
def getVersionFromJava() {
	String versionPrefix = 'public static final String VERSION = "';

	String version = "V0";
	File file = file('src/main/java/com/existingeevee/moretcon/ModInfo.java')
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(versionPrefix)) {
			version = s.substring(versionPrefix.length(), s.length() - 2);
			System.out.println("Version: " + version)
		}
	}
	
	return version;
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
    maven {
        name "CurseMaven"
        url "https://www.cursemaven.com"
    }
    maven {
        name "Baubles"
        url "http://maven.thiakil.com/"
    }
    maven {
        name "Covers1624"
        url "https://maven.covers1624.net/"
    }
    maven {
        name "ChickenBones"
        url "http://chickenbones.net/maven/"
    }
    flatDir {
        dir 'libs'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    
    embed('org.spongepowered:mixin:0.8.3-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }

    implementation  'curse.maven:mantle-74924:2713386'
    implementation  'curse.maven:tinkers-construct-74072:2902483'

    implementation  'com.azanor.baubles:Baubles:1.12-1.5.2'
    implementation  'curse.maven:plusticminusbad-376903:3648592'

    implementation  'curse.maven:aether-255308:3280119'
    implementation  'curse.maven:angry-pixel-the-betweenlands-mod-243363:4479688'
    implementation  'curse.maven:the-twilight-forest-227639:3051450'

    implementation  'curse.maven:libnine-322344:3509087'
    implementation  'curse.maven:tinkers-evolution-384589:3362723'
    implementation  'curse.maven:constructs-armory-287683:3174535'

    implementation  'curse.maven:jei-238222:4538010'

    implementation  'codechicken:CodeChickenLib:1.12.2-3.2.3_devfix.1-universal'

	implementation ("net.minecraftforge:mergetool:0.2.3.3") { force = true }

    /*
    note that to properly build this, the following jars will need to be placed in the ./run/mods/ folder
    
    TheBetweenlands-3.8.1-core.jar - this can be obtained from extracting the betweenlands jar

    */
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':minecraft_version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        refMap = mod_mixin_refmap.replace('${mod_id}', mod_id)
    }
}

mixin {
    config mod_mixin_configs.replace('${mod_id}', mod_id)
}

jar {
    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }

    manifest.attributes(
            'ForceLoadAsMod': 'true',
            'FMLCorePluginContainsFMLMod': 'true',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': '0'
    )
} 

jar.finalizedBy('reobfJar')
